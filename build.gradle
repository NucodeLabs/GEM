import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

group 'ru.nucodelabs'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.1'
}

sourceCompatibility = '16'
targetCompatibility = '16'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'ru.nucodelabs.gem'
    mainClass = 'ru.nucodelabs.gem.App'
}

javafx {
    version = "17"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    //implementation("eu.hansolo.fx:charts:17.0.1")
}

test {
    useJUnitPlatform()
    systemProperty "java.library.path", file("orig/ForwardSolver/build/lib/").absolutePath
}

task forwardSolver(type: Exec) {
    workingDir "orig/ForwardSolver"
    doFirst {
        mkdir "${workingDir}/build/lib"
    }
    if (osdetector.os == "osx") {
        commandLine "/usr/bin/clang",
                "${workingDir}/forwardsolver.cpp",
                "${workingDir}/VESER.C",
                "-O3",
                "-arch",
                "x86_64",
                "-fPIC",
                "-dynamiclib",
                "-I",
                "${workingDir}",
                "-I",
                "${Jvm.current().javaHome}/include",
                "-I",
                "${Jvm.current().javaHome}/include/darwin",
                "-o",
                "${workingDir}/build/lib/libforwardsolver.dylib"
    } else if (osdetector.os == "linux") {
        commandLine "/usr/bin/gcc",
                "${workingDir}/forwardsolver.cpp",
                "${workingDir}/VESER.C",
                "-O3",
                "-fPIC",
                "-shared",
                "-I",
                "${workingDir}",
                "-I",
                "${Jvm.current().javaHome}/include",
                "-I",
                "${Jvm.current().javaHome}/include/linux",
                "-o",
                "${workingDir}/build/lib/libforwardsolver.so"
    } else {
        commandLine "C:\\Program Files\\LLVM\\bin\\clang.exe",
                "${workingDir}\\forwardsolver.cpp",
                "${workingDir}\\VESER.C",
                "-O3",
                "-shared",
                "-I",
                "${workingDir}",
                "-I",
                "${Jvm.current().javaHome}\\include",
                "-I",
                "${Jvm.current().javaHome}\\include\\win32",
                "-o",
                "${workingDir}\\build\\lib\\forwardsolver.dll"
        doLast {
            file("${workingDir}/build/lib/forwardsolver.exp").delete()
            file("${workingDir}/build/lib/forwardsolver.lib").delete()
        }
    }

}

test.dependsOn 'forwardSolver'
